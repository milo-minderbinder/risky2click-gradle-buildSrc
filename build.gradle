plugins {
    id 'groovy-gradle-plugin'
    alias(libs.plugins.springBoot) apply false
}
apply plugin: 'io.spring.dependency-management'

repositories {
    gradlePluginPortal()
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

final String managedSpringBootVersion = dependencyManagement.managedVersions['org.springframework.boot:spring-boot']

dependencies {
    implementation "org.springframework.boot:spring-boot-gradle-plugin:${managedSpringBootVersion}"
    implementation 'io.spring.gradle:dependency-management-plugin'
    implementation plugin(libs.plugins.release)
}

// Helper function that transforms a Gradle Plugin alias from a
// Version Catalog into a valid dependency notation for buildSrc
static Provider<String> plugin(Provider<PluginDependency> plugin) {
    return plugin.map { it.pluginId + ":" + it.pluginId + ".gradle.plugin:" + it.version }
}
