plugins {
    id 'groovy-conventions'
}


if (pluginManager.hasPlugin('io.spring.dependency-management')) {
    dependencyManagement {
        //overriddenByDependencies = false
        testImplementation {
            imports {
                logger.warn("importing ${libs.spock.bom.get()} BOM into dependencyManagement testImplementation configuration")
                mavenBom libs.spock.bom.map { "${it.module}:${it.version}"}.get()
            }
        }
    }
} else {
    dependencies {
        logger.warn("importing platform ${libs.spock.bom.get()} BOM into testImplementation configuration")
        testImplementation platform(libs.spock.bom)
    }
}

dependencies {
    testImplementation 'org.apache.groovy:groovy'
    testImplementation 'org.spockframework:spock-core'
}

pluginManager.withPlugin('org.springframework.boot') {
    dependencies {
        testImplementation 'org.spockframework:spock-spring'
    }
}
